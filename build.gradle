/*
 * Copyright 2021 Heroic Labs
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *     http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

import com.github.jengelman.gradle.plugins.shadow.tasks.ConfigureShadowRelocation
import org.gradle.plugins.ide.eclipse.model.SourceFolder

// dependencies required by the gradle build script itself
buildscript {
    repositories {
        // where to resolve the dependencies
        maven {
            url "https://maven-central.storage-download.googleapis.com/repos/central/data/"
        }
        google()
    }
    dependencies {
        classpath 'com.google.protobuf:protobuf-gradle-plugin:0.8.18'
    }
}

plugins {
    id 'com.github.johnrengelman.shadow' version '5.2.0'
    id 'com.google.protobuf' version '0.8.18'
    id "io.freefair.lombok" version "5.1.1"
    //for distTar which is called when importing this SDK w/ JitPack
    id 'distribution'
    id 'eclipse'
    id 'maven-publish'
    id 'idea'
    id 'java'
    id 'java-library'
}

lombok {
    version = "1.18.12"
}

group = 'com.github.heroiclabs'
version = '2.2.0'

sourceCompatibility = 1.8
targetCompatibility = 1.8

repositories {
    // where to pull dependencies from
    maven {
        url "https://maven-central.storage-download.googleapis.com/repos/central/data/"
    }
    mavenLocal()
    mavenCentral()
}

configurations {
    all {
        exclude group: 'io.grpc', module:'grpc-protobuf'
        exclude group: "com.google.protobuf", module: "protobuf-java"
    }
}

dependencies {
    // syntax sugaring for data models
    // commented out due to io.freefair.lombok setting these by default.
    // annotationProcessor 'org.projectlombok:lombok:1.18.12'
    // implementation "org.projectlombok:lombok:1.18.12"

    implementation 'com.google.code.gson:gson:2.8.9'
    api 'io.grpc:grpc-protobuf-lite:1.56.1'
    // listenable future is part of the SDK public API
    api 'com.google.guava:guava:31.0.1-android'
    implementation 'com.google.protobuf:protobuf-javalite:3.22.3'

    implementation ('io.grpc:grpc-okhttp:1.48.0') {
        exclude group: 'com.squareup.okio', module:'okio'
    }

    implementation 'com.squareup.okio:okio:2.10.0'
    implementation 'com.squareup.okhttp3:okhttp:4.9.3'
    implementation 'javax.annotation:javax.annotation-api:1.3.2'
    implementation 'io.grpc:grpc-netty-shaded:1.43.2'
    implementation 'io.grpc:grpc-stub:1.43.2'
    compileOnly 'org.apache.tomcat:annotations-api:6.0.53'
    implementation 'org.slf4j:slf4j-api:1.7.33'

    protobuf 'com.google.api.grpc:proto-google-common-protos:2.7.1'
    testCompile 'junit:junit:4.13.2'
}

sourceSets {
    nakama {
        java {
            srcDirs "src/main/java/com/heroiclabs/nakama"
        }

        proto {
            exclude 'protoc-gen-swagger/**/*.proto'
        }
    }

    satori {
         java {
            srcDirs "src/main/java/com/heroiclabs/satori"
        }

        proto {
            exclude 'protoc-gen-swagger/**/*.proto'
        }
    }
}

idea {
    module {
        sourceDirs += file("${protobuf.generatedFilesBaseDir}/main/grpc");
        sourceDirs += file("${protobuf.generatedFilesBaseDir}/main/java");
    }
}

/* Disable warnings from generated source code. */
eclipse {
    classpath {
      file {
            whenMerged {

                entries += new SourceFolder("build/generated/source/proto/main/grpc", null)
                entries += new SourceFolder("build/generated/source/proto/main/java", null)

                def grpcSource = entries.find {
                    it.path == "build/generated/source/proto/main/grpc"
                }

                grpcSource.entryAttributes["ignore_optional_problems"] = 'true'

                def javaLiteSource = entries.find {
                    it.path == "build/generated/source/proto/main/java"
                }

                javaLiteSource.entryAttributes["ignore_optional_problems"] = 'true'
            }
        }
    }
}

protobuf {
    protoc {
        artifact = 'com.google.protobuf:protoc:3.19.3'
    }

    plugins {
        grpc {
            artifact = "io.grpc:protoc-gen-grpc-java:1.43.2"
        }
    }

    generateProtoTasks {
        all().each { task ->
            task.builtins {
                java {
                    option "lite"
                }
            }

            task.plugins {
                grpc { option "lite" }
            }
        }
    }
}

jar {
    manifest {
        attributes 'Implementation-Title': 'Nakama client',
                   'Implementation-Version': "$project.version"
    }
    exclude '/**/*.proto'
    from sourceSets.nakama.output
}

jar {
    manifest {
        attributes 'Implementation-Title': 'Satori client',
                   'Implementation-Version': "$project.version"
    }
    exclude '/**/*.proto'
    from sourceSets.satori.output
}

def pomConfig = {
    licenses {
        license {
            name 'The Apache Software License, Version 2.0'
            url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
            distribution 'repo'
        }
    }
    developers {
        developer {
            id 'andreimihu'
            name 'Andrei Mihu'
            email 'andrei@heroiclabs.com'
        }
        developer {
            id 'cmolozian'
            name 'Chris Molozian'
            email 'chris@heroiclabs.com'
        }
        developer {
            id 'mofirouz'
            name 'Mo Firouz'
            email 'mo@heroiclabs.com'
        }
    }
    scm {
        url 'https://github.com/heroiclabs/nakama-java'
    }
}

task nakamaJar(type: Jar) {
    archiveBaseName = 'nakama-java'
    version = project.version
    from sourceSets.nakama.allJava
}

task satoriJar(type: Jar) {
    archiveBaseName = 'satori-java'
    version = project.version
    from sourceSets.satori.allJava
}

publishing {
    publications {
        nakama(MavenPublication) {
            artifact nakamaJar {
                classifier 'sources'
            }
            artifactId 'nakama-java'
            groupId "$project.group"
            version "$project.version"

            pom.withXml {
                def root = asNode()
                root.appendNode('description', 'Java client for Nakama server.')
                root.appendNode('name', 'Nakama client')
                root.appendNode('url', 'https://heroiclabs.com/docs/android-java-client-guide/') // TODO update this
                root.children().last() + pomConfig
            }
        }

        satori(MavenPublication) {
            artifact satoriJar {
                classifier 'sources'
            }
            artifactId 'satori-java'
            groupId "$project.group"
            version "$project.version"

            pom.withXml {
                def root = asNode()
                root.appendNode('description', 'Java client for Satori server.')
                root.appendNode('name', 'Satori client')
                root.appendNode('url', 'https://heroiclabs.com/docs/android-java-client-guide/') // TODO update this
                root.children().last() + pomConfig
            }
        }

        shadow(MavenPublication) {
            project.shadow.component(it)
            artifactId "${tasks.shadowJar.baseName}-all"

            // override default classifier of 'all'
            it.artifacts[0].classifier = ''
        }
    }
}

task nakamaShadowJar(type: com.github.jengelman.gradle.plugins.shadow.tasks.ShadowJar) {
    archiveClassifier = 'all'
    from sourceSets.nakama.output
}

task satoriShadowJar(type: com.github.jengelman.gradle.plugins.shadow.tasks.ShadowJar) {
    archiveClassifier = 'all'
    from sourceSets.satori.output
}

task relocateShadowJar(type: ConfigureShadowRelocation) {
    target = tasks.shadowJar
    prefix = "heroiclabs" // prefix all classpath entries with `heroiclabs`
}

tasks.shadowJar.dependsOn tasks.relocateShadowJar
nakamaShadowJar.dependsOn compileNakamaJava
satoriShadowJar.dependsOn compileSatoriJava

javadoc {
    destinationDir = file("docs")
    options.addBooleanOption("Xdoclint:-missing", true)
}

test {
    testLogging.showStandardStreams = true
    testLogging.exceptionFormat = 'full'
}
